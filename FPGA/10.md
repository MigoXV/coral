# 10 UART

```verilog
//UART.v
module UART( 	sys_rst_l,
                  sys_clk, uart_clk,
                  mp_clk,	mp_cs_l,
                  mp_addx,
                  mp_data_to_uart,
                  mp_data_from_uart,
                  mp_rd_l,
                  mp_wr_l,
                  mp_int_l,
                  uart_XMIT_dataH,
                  uart_REC_dataH,
                  stat_rec_dataH,
                  stat_xmit_emptyH
               );
`include "uart_inc.vh"
    input sys_rst_l;
    input sys_clk;
    output uart_clk;
    input mp_clk;
    input mp_cs_l;
    input [2:0] mp_addx;
    input [7:0] mp_data_to_uart;
    output [7:0] mp_data_from_uart;
    input  mp_rd_l;
    input mp_wr_l;
    output mp_int_l;
    output  uart_XMIT_dataH;
    input  uart_REC_dataH;
    output  stat_rec_dataH;
    output  stat_xmit_emptyH;
    wire uart_clk;
    wire [15:0]baud_rate_div;
    wire xmit_start_pulseH;
    wire xmit_doneH;
    wire [7:0]reg_xmit_dat;
    wire xmit_busyH;
    wire [7:0]rec_dataH;
    wire rec_readyH;
    wire stat_rec_dataH;
    wire stat_xmit_emptyH;
    mp_int iMP_INT(
               .sys_rst_l(sys_rst_l),    	.uart_clk(uart_clk),
               .mp_clk(mp_clk),    	.mp_cs_l(mp_cs_l),
               .mp_addx(mp_addx),    	.mp_data_from_uart(mp_data_from_uart),
               .mp_data_to_uart(mp_data_to_uart),
               .mp_rd_l(mp_rd_l),    	.mp_wr_l(mp_wr_l),
               .mp_int_l(mp_int_l),
               .baud_rate_div(baud_rate_div),
               .start_pulseH(xmit_start_pulseH),
               .xmit_doneH(xmit_doneH),    	.reg_xmit_dat(reg_xmit_dat),
               .xmit_busyH(xmit_busyH),
               .rec_dataH(rec_dataH),    	.rec_readyH(rec_readyH),
               .stat_rec_dataH(stat_rec_dataH),
               .stat_xmit_emptyH(stat_xmit_emptyH)      );
    u_xmit  iXMIT(
                .sys_clk(uart_clk),
                .sys_rst_l(sys_rst_l),
                .uart_xmitH(uart_XMIT_dataH),
                .xmitH(xmit_start_pulseH),
                .xmit_dataH(reg_xmit_dat),
                .xmit_doneH(xmit_doneH)     );

    u_rec iRECEIVER(
              .sys_rst_l(sys_rst_l),
              .sys_clk(uart_clk),
              .uart_dataH(uart_REC_dataH),
              .rec_dataH(rec_dataH),
              .rec_readyH(rec_readyH)      );
    baud iBAUD(
             .sys_clk(sys_clk),
             .sys_rst_l(sys_rst_l),
             .baud_clk(uart_clk),
             .baud_rate_div(baud_rate_div)
         );

endmodule

```

```verilog
//uart_inc.h
parameter 	r_START=3'b001,
           r_CENTER=3'b010,
           r_WAIT=3'b011,
           r_SAMPLE=3'b100,
           r_STOP=3'b101;
parameter 	x_IDLE=3'b000, x_START=3'b010, x_WAIT=3'b011,x_SHIFT=3'b100,
           x_STOP=3'b101;
parameter 	x_STARTbit=2'b00, x_STOPbit=2'b01, x_ShiftReg = 2'b10;
parameter 	LO=1'b0, HI=1'b1, X=1'bx;
parameter 	WORD_LEN=8;
parameter 	XTAL_CLK=53125000;
parameter 	BAUD=2400;
parameter 	CLK_DIV=XTAL_CLK/(BAUD*16*2);
parameter 	CW=10;
parameter 	SEL_XMIT_DAT=3'b000,
           SEL_REC_DAT        =3'b000,
           SEL_BAUD_REG_LO    =3'b001,
           SEL_BAUD_REG_HI    =3'b010,
           SEL_STAT_REG       =3'b011,
           SEL_INT_STAT_REG   =3'b100,
           SEL_INT_ENA_REG    =3'b101;
```

```verilog
//MP_INT.v
module mp_int(
        //系统信号
        sys_rst_l, uart_clk, mp_clk,
        //微处理器接口
        mp_cs_l, mp_addx, mp_data_to_uart, mp_data_from_uart,    mp_rd_l, mp_wr_l, mp_int_l,
        //波特率变换模块接口
        baud_rate_div,
        //UART发送模块接口
        start_pulseH, xmit_doneH, reg_xmit_dat, xmit_busyH,
        // UART接收模块接口
        rec_dataH, rec_readyH, xmit_busy_rstH, stat_xmit_emptyH,
        stat_rec_dataH,
        // 调试接口
        wr_done_pulseH, sel_xmit_datH      );
`include "uart_inc.vh"

    input    sys_rst_l;
    input    mp_clk;
    input    uart_clk;
    input    mp_cs_l;
    input    [2:0]mp_addx;
    input    mp_rd_l;
    input    mp_wr_l;
    input    [7:0]mp_data_to_uart;
    output   [7:0]mp_data_from_uart;
    output   [15:0]baud_rate_div;
    output   start_pulseH;
    input    xmit_doneH;
    output   xmit_busyH;
    output   [7:0]reg_xmit_dat;
    input    [7:0]rec_dataH;
    input    rec_readyH;
    output   mp_int_l;
    output   xmit_busy_rstH;
    output   stat_xmit_emptyH;
    output   stat_rec_dataH;
    output   wr_done_pulseH;
    output   sel_xmit_datH;
    reg      [7:0]reg_baud_rate_div_lo_in;
    reg      [7:0]reg_baud_rate_div_hi;
    reg      [7:0]reg_baud_rate_div_lo;
    reg      [7:0]reg_xmit_dat;
    reg      [7:0]reg_int_ena;
    reg      xmit_busyH;
    reg      [7:0]mp_data_out;
    reg      stat_rec_dataH;
    reg      stat_xmit_emptyH;
    reg      rec_new_dataH;
    wire     [7:0]mp_data;
    wire     sel_baud_regLoH;
    wire     sel_baud_regHiH;
    wire     sel_xmit_datH;
    wire     sel_int_maskH;
    wire     wr_done_pulseH;
    wire     xmit_busyH_sync;
    wire     start_pulseH;
    wire     xmit_doneH_sync;
    wire     [15:0]baud_rate_div;
    wire     xmit_busy_rstL;
    wire     rec_readyH_sync;
    wire     rec_ready_pulseH;
    wire     sel_int_stat_regH;
    assign mp_data           = mp_data_to_uart;
    assign mp_data_from_uart = mp_data_out;
    assign baud_rate_div     = 			//波特率输出
           {reg_baud_rate_div_hi,reg_baud_rate_div_lo};
    assign sel_baud_regLoH   = ~ mp_cs_l &  //波特率低位
           (mp_addx==SEL_BAUD_REG_LO);
    assign sel_baud_regHiH   = ~ mp_cs_l &  //波特率高位
           (mp_addx==SEL_BAUD_REG_HI);
    assign sel_xmit_datH     = ~ mp_cs_l &  // 发送数据
           (mp_addx==SEL_XMIT_DAT);
    assign sel_int_maskH     = ~ mp_cs_l &  //中断使能
           (mp_addx==SEL_INT_ENA_REG);
    assign sel_int_stat_regH = ~ mp_cs_l &  // 中断状态
           (mp_addx==SEL_INT_STAT_REG);
    //写寄存器
    always@ (posedge mp_clk or negedge sys_rst_l)
        if (~sys_rst_l) begin
            reg_baud_rate_div_lo    <= 0;
            reg_baud_rate_div_hi    <= 0;
            reg_xmit_dat            <= 0;
            reg_int_ena             <= 0;
        end
        else begin
            if (~mp_wr_l) begin
                if (sel_baud_regLoH)
                    reg_baud_rate_div_lo_in <= mp_data;
                if (sel_baud_regHiH) begin
                    reg_baud_rate_div_hi <= mp_data;
                    reg_baud_rate_div_lo <= reg_baud_rate_div_lo_in;
                end
                if (sel_xmit_datH)
                    reg_xmit_dat <= mp_data;
                if (sel_int_maskH)
                    reg_int_ena <= mp_data;
            end
        end
    //读寄存器
    always@ (mp_addx or rec_dataH or reg_baud_rate_div_lo or reg_baud_rate_div_hi or stat_rec_dataH or stat_xmit_emptyH or reg_int_ena) begin
        case (mp_addx)
            SEL_REC_DAT:
                mp_data_out=rec_dataH;
            SEL_BAUD_REG_LO:
                mp_data_out=reg_baud_rate_div_lo;
            SEL_BAUD_REG_HI:
                mp_data_out=reg_baud_rate_div_hi;
            SEL_STAT_REG:
                mp_data_out={rec_new_dataH,xmit_busyH};
            SEL_INT_STAT_REG:
                mp_data_out={stat_rec_dataH,stat_xmit_emptyH};
            SEL_INT_ENA_REG:
                mp_data_out=reg_int_ena;
            default:
                mp_data_out=8'hxx;
        endcase
    end
    // 产生中断
    assign mp_int_l=~((stat_xmit_emptyH & reg_int_ena[0]) |
                      (stat_rec_dataH   & reg_int_ena[1]));
    // 接受数据有效，对应STAT_REG[1]
    always@ (posedge mp_clk or negedge sys_rst_l) begin
        if (~sys_rst_l)
            rec_new_dataH <= 0;
        else if (rec_ready_pulseH) 	//接受到一个字节数据
            rec_new_dataH <= 1'b1;
        else if (sel_xmit_datH & mp_rd_l)  //读取了接受到的数据
            rec_new_dataH <= 1'b0;
        //接受完成及发送空闲,对应INT_STAT_REG[1:0]
        if (~sys_rst_l)   begin
            stat_rec_dataH    <= 1'b0;
            stat_xmit_emptyH  <= 1'b1;
        end
        else begin
            if (sel_int_stat_regH & ~ mp_wr_l) // 通过写清除
                stat_xmit_emptyH <=mp_data[0];
            else if (xmit_busy_rstH) 				//完成一个字节发送
                stat_xmit_emptyH <= 1'b1;
            else if (sel_xmit_datH & ~ mp_wr_l) //写入待发送数据
                stat_xmit_emptyH <= 0;

            if (sel_int_stat_regH & ~ mp_wr_l) // 通过写清除
                stat_rec_dataH <= mp_data[1];
            else if (rec_ready_pulseH) //接受到一个字节数据，产生中断
                stat_rec_dataH <= 1'b1;
        end
    end
    assign xmit_busy_rstL=~xmit_busy_rstH; //发送结束
    one_shot OS_WR (
                 .clk_in(mp_clk),
                 .sys_rst_l(sys_rst_l),
                 .d(~mp_wr_l),
                 .q(wr_done_pulseH) );

    wire rst= sys_rst_l & xmit_busy_rstL; //多余

    //向发送模块启动发送,对应STAT_REG[0]
    always@ (posedge mp_clk or negedge xmit_busy_rstL)
        if (~ xmit_busy_rstL)
            xmit_busyH <= 0;
        else if (wr_done_pulseH & sel_xmit_datH)
            // else if ( sel_xmit_datH)
            xmit_busyH <= 1;
    sync SYNC_WR (.
                  clk_in(uart_clk),                 // 2级同步
                  .sys_rst_l(xmit_busy_rstL),
                  .d(xmit_busyH),
                  .q(xmit_busyH_sync) );

    one_shot OS_WR2 (				//检测上升沿
                 .clk_in(uart_clk),
                 .sys_rst_l(sys_rst_l),
                 .d(xmit_busyH_sync),
                 .q(start_pulseH) );
    // 接受发送结束信号
    sync SYNC_WR2 (
             .clk_in(mp_clk),    				 // 2级同步
             .sys_rst_l(sys_rst_l),
             .d(xmit_doneH),
             .q(xmit_doneH_sync) );

    one_shot OS_WR3 (				//检测上升沿
                 .clk_in(mp_clk),
                 .sys_rst_l(sys_rst_l),
                 .d(xmit_doneH_sync),
                 .q(xmit_busy_rstH) );
    //接受到接受数据信号
    sync SYNC_RECDAT(
             .clk_in(mp_clk),						 // 2级同步
             .sys_rst_l(sys_rst_l),
             .d(rec_readyH),
             .q(rec_readyH_sync) );

    one_shot OS_RECDAT(				//检测上升沿
                 .clk_in(mp_clk),
                 .sys_rst_l(sys_rst_l),
                 .d(rec_readyH_sync),
                 .q(rec_ready_pulseH) );

endmodule

```

```verilog
//BAUD.v

module baud(sys_clk,sys_rst_l,baud_clk,baud_rate_div);
`include "uart_inc.vh"

    input sys_clk;
    input sys_rst_l;
    output baud_clk;
    input [15:0]baud_rate_div;
    reg   [15:0] clk_div;
    reg   baud_clk;
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l) begin
            clk_div <=0;
            baud_clk <=0;
        end
        else if (clk_div ==baud_rate_div) begin
            clk_div  <=0;
            baud_clk <=~baud_clk;
        end
        else begin
            clk_div  <=clk_div+ 1;
            baud_clk <=baud_clk;
        end
endmodule


```

```verilog
//SYNC.V
module sync (clk_in,sys_rst_l, d, q);  
	input  clk_in,sys_rst_l, d;  
	output q;  
	reg  pipe1, pipe2;
	always@ (posedge clk_in or negedge sys_rst_l)
		 if (~sys_rst_l) begin 
			pipe1 <= 0; 
			pipe2 <= 0; 
		end  
		else begin 
			pipe1 <= d;
			pipe2 <= pipe1; 
		end
	assign q= pipe2;
endmodule
//ONE_SHOT.v
module one_shot (sys_rst_l, clk_in, d, q);  
	input  sys_rst_l;  
	input  clk_in;  
	input  d;  
	output q;  
	reg    d_del;
	always@ (posedge clk_in or negedge sys_rst_l)  
		if (~sys_rst_l) 
			d_del <= 1'b1;  
		else d_del <= ~d;

	assign q=d & d_del;

endmodule
```

```verilog
module u_rec (
        sys_rst_l,
        sys_clk,
        uart_dataH,
        rec_dataH,
        rec_readyH);
`include "uart_inc.vh"

    input sys_rst_l;
    input sys_clk;
    input uart_dataH;
    output [7:0] rec_dataH;
    output rec_readyH;
    reg [2:0] next_state,state;
    reg [3:0] recd_bitCntrH;
    reg rec_datH, rec_datSyncH;
    reg [3:0] bitCell_cntrH;
    reg cntr_resetH;
    reg countH;
    reg rstCountH;
    reg [7:0]par_dataH;
    reg shiftH;
    reg rec_readyInH;
    reg rec_readyH;
    // reg rec_reaydInH;
    wire [7:0] rec_dataH;
    assign rec_dataH=par_dataH;
    //两级同步，消除亚稳态
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l) begin
            rec_datSyncH <= 1;
            rec_datH <= 1;
        end
        else begin
            rec_datSyncH <= uart_dataH;
            rec_datH <= rec_datSyncH;
        end

    // 位元计数器
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l)
            bitCell_cntrH <= 0;
        else if (cntr_resetH)
            bitCell_cntrH <= 0;
        else
            bitCell_cntrH <= bitCell_cntrH+ 1;
    //串行输入移位寄存器
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l)
            par_dataH <= 0;
        else if(shiftH) begin
            par_dataH[6:0] <=par_dataH[7:1];
            par_dataH[7]   <=rec_datH;
        end
    // 已接受位数计数器
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l)
            recd_bitCntrH <= 0;
        else if(countH)
            recd_bitCntrH <=recd_bitCntrH+1;
        else if(rstCountH)
            recd_bitCntrH <= 0;
    // 状态机
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l)
            state <=r_START;
        else
            state <= next_state;
    always@ (state or rec_datH or bitCell_cntrH or recd_bitCntrH )  begin
        next_state  =state;
        cntr_resetH =HI;
        shiftH      =LO;
        countH      =LO;
        rstCountH   =LO;
        rec_readyInH=LO;
        case (state)
            r_START: begin
                if (~ rec_datH )
                    next_state=r_CENTER;
                else begin
                    next_state=r_START;
                    rstCountH=HI;
                    // rec_readyInH=HI;
                end
            end
            r_CENTER: begin
                if (bitCell_cntrH == 4'h4) begin
                    if (~ rec_datH)
                        next_state=r_WAIT;
                    else
                        next_state=r_START;
                end
                else begin
                    next_state=r_CENTER;
                    cntr_resetH=LO;
                end
            end
            r_CENTER: begin
                if (bitCell_cntrH == 4'h4) begin
                    if (~ rec_datH)
                        next_state=r_WAIT;
                    else
                        next_state=r_START;
                end
                else begin
                    next_state=r_CENTER;
                    cntr_resetH=LO;
                end
            end
            r_WAIT: begin
                if(bitCell_cntrH==4'hE) begin
                    if (recd_bitCntrH==WORD_LEN)
                        next_state=r_STOP;
                    else
                        next_state=r_SAMPLE;
                end
                else begin
                    next_state=r_WAIT;
                    cntr_resetH=LO;
                end
            end
            r_SAMPLE: begin
                shiftH=HI;
                countH=HI;
                next_state=r_WAIT;
            end
            r_STOP: begin
                next_state=r_START;
                rec_readyInH=HI;
            end
            default: begin
                next_state    =3'bxxx;
                cntr_resetH   =X;
                shiftH        =X;
                countH        =X;
                rstCountH     =X;
                rec_readyInH  =X;
            end
        endcase
    end
    //向异时钟域传送时，必须保存后输出，消除危险路径
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l)
            rec_readyH <=0;
        else
            rec_readyH <=rec_readyInH;

endmodule

```

```verilog
//u_xmit.v
module u_xmit(
        sys_clk,
        sys_rst_l,
        uart_xmitH,
        xmitH,
        xmit_dataH,
        xmit_doneH
    );
`include "uart_inc.vh"

    input sys_clk;
    input sys_rst_l;
    output uart_xmitH;
    input xmitH;
    input[7:0] xmit_dataH;
    output xmit_doneH;
    reg [2:0] next_state, state;
    reg load_shiftRegH;
    reg shiftEnaH;
    reg [4:0]bitCell_cntrH;
    reg countEnaH;
    reg [7:0]xmit_ShiftRegH;
    reg [3:0]bitCountH;
    reg rst_bitCountH;
    reg ena_bitCountH;
    reg [1:0]xmitDataSelH;
    reg uart_xmitH;
    reg xmit_doneInH;
    reg xmit_doneH;

    // 多路选择器
    always@ (xmit_ShiftRegH or xmitDataSelH)
    case (xmitDataSelH)
        x_STARTbit:
            uart_xmitH=LO;
        x_STOPbit:
            uart_xmitH=HI;
        x_ShiftReg:
            uart_xmitH=xmit_ShiftRegH[0];
        default:
            uart_xmitH=X;
    endcase
    // 位元计数器
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l)
            bitCell_cntrH <=0;
        else if (countEnaH)
            bitCell_cntrH <=bitCell_cntrH+ 1;
        else
            bitCell_cntrH <=0;
    //移位寄存器
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l)
            xmit_ShiftRegH <=0;
        else  if (load_shiftRegH)
            xmit_ShiftRegH <=xmit_dataH;
        else if (shiftEnaH) begin
            xmit_ShiftRegH[6:0]  <=xmit_ShiftRegH[7:1];
            xmit_ShiftRegH[7]    <=HI;
        end
        else
            xmit_ShiftRegH   <=xmit_ShiftRegH;
    //已发送位计数器
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l)
            bitCountH <=0;
        else if (rst_bitCountH)
            bitCountH <=0;
        else if (ena_bitCountH)
            bitCountH <=bitCountH+ 1;
    // 状态机
    always@ (posedge sys_clk or negedge sys_rst_l)
        if (~sys_rst_l)
            state <=x_IDLE;
        else
            state <=next_state;
    always@ (state or xmitH or bitCell_cntrH or bitCountH) begin
        next_state        =state;
        load_shiftRegH    =LO;
        countEnaH         =LO;
        shiftEnaH         =LO;
        rst_bitCountH     =LO;
        ena_bitCountH     =LO;
        xmitDataSelH      =x_STOPbit;
        xmit_doneInH      =LO;
        case (state)
            x_IDLE: begin
                if (xmitH) begin
                    next_state=x_START;
                    load_shiftRegH=HI;
                end
                else begin
                    next_state=x_IDLE;
                    rst_bitCountH=HI;
                    xmit_doneInH=HI;
                end
            end
            x_START: begin
                xmitDataSelH=x_STARTbit;
                if (bitCell_cntrH ==4'hf)
                    next_state=x_WAIT;
                else begin
                    next_state=x_START;
                    countEnaH=HI;
                end
            end
            x_WAIT: begin
                xmitDataSelH=x_ShiftReg;
                if (bitCell_cntrH ==4'hE) begin
                    if(bitCountH ==WORD_LEN-1)
                        next_state=x_STOP;
                    else begin
                        next_state=x_SHIFT;
                        ena_bitCountH=HI;
                    end
                end
                else begin
                    next_state=x_WAIT;
                    countEnaH=HI;
                end
            end
            x_SHIFT: begin
                xmitDataSelH=x_ShiftReg;
                next_state=x_WAIT;
                shiftEnaH=HI;
            end
            x_STOP: begin
                xmitDataSelH=x_STOPbit;
                if (bitCell_cntrH ==4'hF) begin
                    next_state=x_IDLE;
                    xmit_doneInH=HI;
                end
                else begin
                    next_state=x_STOP;
                    countEnaH=HI;
                end
            end

            default: begin
                next_state      =3'bxxx;
                load_shiftRegH  =X;
                countEnaH       =X;
                shiftEnaH       =X;
                rst_bitCountH   =X;
                ena_bitCountH   =X;
                xmitDataSelH    =2'bxx;
                xmit_doneInH    =X;
            end
        endcase
    end
    //向异时钟域传送时，必须保存后输出，消除危险路径	
    always@ (posedge sys_clk or negedge sys_rst_l)
    if (~sys_rst_l)
        xmit_doneH <=0;
    else
        xmit_doneH <=xmit_doneInH;
endmodule

```

